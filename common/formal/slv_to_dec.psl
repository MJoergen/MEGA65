vunit i_slv_to_dec(slv_to_dec(synthesis))
{
   -- set all declarations to run on clk_i
   default clock is rising_edge(clk_i);

   -- Additional signals used during formal verification
   signal f_s_data  : std_logic_vector(G_DATA_SIZE - 1 downto 0);
   signal f_s_valid : std_logic;
   signal f_m_data  : integer;

   -- Process inputs
   input_proc : process (clk_i)
   begin
      if rising_edge(clk_i) then
         if s_valid_i and s_ready_o then
            f_s_data  <= s_data_i;
            f_s_valid <= '1';
         end if;

         if rst_i then
            f_s_valid <= '0';
         end if;
      end if;
   end process input_proc;

   -- Process outputs
   output_proc : process (clk_i)
   begin
      if rising_edge(clk_i) then

         if m_valid_o and m_ready_i then
            f_m_data <= f_m_data*10 + to_integer(m_data_o);
         end if;

         if rst_i then
            f_m_data <= 0;
         end if;
      end if;
   end process output_proc;


   --------------------------------------------
   -- ASSERTIONS ABOUT OUTPUTS
   --------------------------------------------

   -- Output must be empty after reset
   f_after_reset : assert always {rst_i} |=> {not m_valid_o};

   -- Output must be stable until accepted
   f_output_stable : assert always {m_valid_o and not m_ready_i and not rst_i} |=>
      {stable(m_valid_o) and stable(m_data_o)};

   -- Output must be in range
   f_output_range : assert always {m_valid_o and not rst_i} |-> {m_data_o < 10};

   -- Verify correct output
   f_res : assert always {m_valid_o and m_last_o and not rst_i} |->
      {f_s_valid = '1' and f_m_data = to_integer(f_s_data)};


   --------------------------------------------
   -- ASSUMPTIONS ABOUT INPUTS
   --------------------------------------------

   -- Require reset at startup.
   f_reset : assume {rst_i};

   f_input_stable : assume always {s_valid_i and not s_ready_o and not rst_i} |=>
      {stable(s_valid_i) and stable(s_data_i)};


   --------------------------------------------
   -- COVER STATEMENTS TO VERIFY REACHABILITY
   --------------------------------------------

   f_full_to_empty : cover {m_valid_o and m_last_o and not rst_i};

} -- vunit i_slv_to_dec(slv_to_dec(synthesis))

